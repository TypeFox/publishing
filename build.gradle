/*
 * Publishing script for open-source projects
 * Copyright 2016 TypeFox GmbH
 */

//--------------------------------------------------------------------
// Source repositories for fetching artifacts built by Jenkins

if (!hasProperty('publish.branch')) {
	if (hasProperty('PUBLISH_BRANCH'))
		ext.'publish.branch' = property('PUBLISH_BRANCH')
	else
		ext.'publish.branch' = 'master'
}

def jenkinsRepo = { jobName ->
	"http://services.typefox.io/open-source/jenkins/job/$jobName/lastStableBuild/artifact/build/maven-repository/"
}
def jenkinsPipelineRepo = { jobName ->
	"http://services.typefox.io/open-source/jenkins/job/$jobName/job/${property('publish.branch')}/lastStableBuild/artifact/build/maven-repository/"
}
repositories {
	maven { url jenkinsRepo('lsapi') }
	maven { url jenkinsPipelineRepo('xtext-lib') }
	maven { url jenkinsPipelineRepo('xtext-core') }
	maven { url jenkinsPipelineRepo('xtext-extras') }
	maven { url jenkinsPipelineRepo('xtext-web') }
	maven { url jenkinsPipelineRepo('xtext-maven') }
	maven { url jenkinsPipelineRepo('xtext-xtend') }
	jcenter()
}

//--------------------------------------------------------------------
// Configuration of publishing plugin

apply plugin: io.typefox.publishing.PublishingPlugin.class

apply from: "artifacts.gradle"
