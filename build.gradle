/*
 * Publishing script for open-source projects
 * Copyright 2016 TypeFox GmbH
 */

apply plugin: 'io.typefox.osspub'
apply from: 'artifacts.gradle'

if (!hasProperty('JENKINS_URL')) {
  ext.JENKINS_URL = 'http://services.typefox.io/open-source/jenkins'
}

def mavenSource = findProperty('mavenSource')
if (mavenSource == null || mavenSource == 'jenkins') {
	def jenkinsRepo = { jobName ->
		"$JENKINS_URL/job/${jobName}/lastStableBuild/artifact/build/maven-repository/"
	}
	def jenkinsPipelineRepo = { jobName ->
		"$JENKINS_URL/job/${jobName}/job/${osspub.branch}/lastStableBuild/artifact/build/maven-repository/"
	}
	repositories {
		maven { url jenkinsPipelineRepo('lsp4j') }
		maven { url jenkinsPipelineRepo('xtext-lib') }
		maven { url jenkinsPipelineRepo('xtext-core') }
		maven { url jenkinsPipelineRepo('xtext-extras') }
		maven { url jenkinsPipelineRepo('xtext-web') }
		maven { url jenkinsPipelineRepo('xtext-maven') }
		maven { url "$JENKINS_URL/job/xtext-xtend/job/${osspub.branch}/lastSuccessfulBuild/artifact/build/maven-repository/" }
		maven { url jenkinsPipelineRepo('xtext-jflex') }
		maven { url "$JENKINS_URL/job/yang-lsp/job/${osspub.branch}/lastStableBuild/artifact/yang-lsp/build/maven-repository/" }
		maven { url "https://ci.eclipse.org/sprotty/job/sprotty-server/job/${osspub.branch}/lastStableBuild/artifact/build/maven-repository/" }
	}
} else if (mavenSource == 'hudsonSnapshot') {
	repositories {
		maven {
			url "https://hudson.eclipse.org/xtext/job/xtext-snapshots/lastStableBuild/artifact/build-result/maven-repository/"
		}
		maven {
			url "https://hudson.eclipse.org/lsp4j/job/lsp4j-snapshots/lastStableBuild/artifact/build-result/maven-repository/"
		}
	}
} else if (mavenSource == 'hudsonRelease') {
	repositories {
		maven {
			url "https://hudson.eclipse.org/xtext/job/xtext-release/lastStableBuild/artifact/build-result/maven-repository/"
		}
		maven {
			url "https://hudson.eclipse.org/lsp4j/job/lsp4j-release/lastStableBuild/artifact/build-result/maven-repository/"
		}
	}
} else {
	throw new GradleException("Invalid mavenSource: $mavenSource")
}
repositories.jcenter()

osspub {
	mavenUploadRepository 'ossrh'

	p2Repository {
		name 'Xtext'
		group 'modeling.tmf.xtext'
		url "$JENKINS_URL/job/xtext-umbrella/job/${osspub.branch}/lastStableBuild/artifact/build/org.eclipse.xtext.sdk.p2-repository-${osspub.version}.zip"
		namespace 'org.eclipse.xtext'
		namespace 'org.eclipse.xtend'
		namespace 'org.eclipse.lsp4j'
		referenceFeature 'org.eclipse.xtext.sdk'
		acceptDifferingJars 'org.eclipse.xtend.core'
		acceptDifferingJars 'org.eclipse.xtend.ide.common'
	}

	p2Repository {
		name 'Lsp4j'
		group 'technology.lsp4j'
		url "$JENKINS_URL/job/lsp4j/job/${osspub.branch}/lastStableBuild/artifact/build/lsp4j.p2-repository-${osspub.version}.zip"
		deployPath 'lsp4j'
		namespace 'org.eclipse.lsp4j'
		referenceFeature 'org.eclipse.lsp4j.sdk'
	}
}
