/* 
 * Publishing script for open-source projects
 * Copyright 2016 TypeFox GmbH
 */

if (!hasProperty('publish.version'))
	throw new ProjectConfigurationException("The version to be published has to be set with -Ppublish.version=<version>", null)
if (!hasProperty('publish.branch'))
	ext.'publish.branch' = 'master'
version = property('publish.version')


//--------------------------------------------------------------------
// Source repositories for fetching artifacts built by Jenkins

def jenkinsRepo = { jobName ->
	"http://services.typefox.io/open-source/jenkins/job/$jobName/lastStableBuild/artifact/build/maven-repository/"
}
def jenkinsPipelineRepo = { jobName ->
	"http://services.typefox.io/open-source/jenkins/job/$jobName/job/${property('publish.branch')}/lastStableBuild/artifact/build/maven-repository/"
}
repositories {
	maven { url jenkinsRepo('lsapi') }
	maven { url jenkinsPipelineRepo('xtext-lib') }
	maven { url jenkinsPipelineRepo('xtext-core') }
	maven { url jenkinsPipelineRepo('xtext-extras') }
	maven { url jenkinsPipelineRepo('xtext-web') }
	jcenter()
}

apply from: "artifacts.gradle"

//--------------------------------------------------------------------
// Definition and configuration of dependencies, artifacts, and tasks

apply plugin: 'signing'
if (hasProperty('SIGNING_SECRETKEYRINGFILE'))
	ext.'signing.secretKeyRingFile' = property('SIGNING_SECRETKEYRINGFILE')
if (hasProperty('SIGNING_KEYID'))
	ext.'signing.keyId' = property('SIGNING_KEYID')
if (hasProperty('SIGNING_PASSWORD'))
	ext.'signing.password' = property('SIGNING_PASSWORD')
def doSigning = !hasProperty('signing.skip') || property('signing.skip') != 'true'

apply plugin: 'maven-publish'
if (hasProperty('PUBLISHING_USERNAME'))
	ext.'publishing.userName' = property('PUBLISHING_USERNAME')
if (hasProperty('PUBLISHING_PASSWORD'))
	ext.'publishing.password' = property('PUBLISHING_PASSWORD')
def isSnapshot = property('publish.version').endsWith('-SNAPSHOT')
publishing.repositories {
	maven {
		if (isSnapshot)
			url "https://oss.sonatype.org/content/repositories/snapshots/"
		else
			url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
		credentials {
			username = findProperty('publishing.userName')
			password = findProperty('publishing.password')
		}
	}
}

def classifiersExtensions = [ new Tuple(null, 'jar'), new Tuple('sources', 'jar'), new Tuple('javadoc', 'jar'), new Tuple(null, 'pom') ]
def getArtifactFileName = { artifactName, classifierName, extensionName ->
	if (classifierName)
		"$buildDir/artifacts/$artifactName-${property('publish.version')}-${classifierName}.$extensionName"
	else
		"$buildDir/artifacts/$artifactName-${property('publish.version')}.$extensionName"
}

// Create separate configurations for each source repository
artifactsMap.entrySet().each { artifactsEntry ->
	def dependenciesConfig = configurations.create("dependencies${artifactsEntry.key}")
	def archivesConfig = configurations.create("archives${artifactsEntry.key}")
	def signaturesConfig = configurations.create("signatures${artifactsEntry.key}")
	
	// Step 1: Specify dependencies to Jenkins artifacts
	artifactsEntry.value.each { artifactName ->
		def groupName = artifactName.startsWith('org.eclipse.xtend') ? 'org.eclipse.xtend' : 'org.eclipse.xtext'
		classifiersExtensions.each { ceTuple ->
			dependencies.add(dependenciesConfig.name,
				[group: groupName, name: artifactName, version: property('publish.version'), classifier: ceTuple[0], ext: ceTuple[1]]
			)
		}
	}
	
	// Step 2: Copy the dependencies to the local build folder
	def copyTask = task "copy${artifactsEntry.key}" (type: Copy) {
		description "Copy the built artifacts of ${artifactsEntry.key} into the build folder"
		from dependenciesConfig
		into "$buildDir/artifacts"
		artifactsEntry.value.each { artifactName ->
			include "**/$artifactName-${project.property('publish.version')}*.jar"
			include "**/$artifactName-${project.property('publish.version')}.pom"
		}
	}
	
	artifactsEntry.value.each { artifactName ->
		classifiersExtensions.each { ceTuple ->
			def archiveFile = file(getArtifactFileName(artifactName, ceTuple[0], ceTuple[1]))
			artifacts.add(archivesConfig.name, archiveFile) {
				builtBy copyTask
			}
		}
	}
	
	// Step 3: Sign the local artifacts
	if (doSigning) {
		signing.sign archivesConfig
		def signTask = project.getTasksByName("signArchives${artifactsEntry.key}", false).first()
		
		artifactsEntry.value.each { artifactName ->
			classifiersExtensions.each { ceTuple ->
				def signatureFile = file(getArtifactFileName(artifactName, ceTuple[0], ceTuple[1]) + '.asc')
				signTask.outputs.file signatureFile
				artifacts.add(signaturesConfig.name, signatureFile) {
					name artifactName
					classifier ceTuple[0]
					builtBy signTask
				}
			}
		}
	}
	
	// Step 4: Create a publication for each project containing all artifacts and their signatures
	artifactsEntry.value.each { artifactName ->	
		publishing.publications.create(artifactName.replace('.', ''), MavenPublication) { publication ->
			archivesConfig.artifacts.matching { artifact ->
				artifact.name == artifactName
			}.each { artifact ->
				publication.artifact artifact
			}
			if (doSigning) {
				signaturesConfig.artifacts.matching { artifact ->
					artifact.name == artifactName
				}.each { artifact ->
					publication.artifact artifact
				}
			}
		}
	}
	
	task "publish${artifactsEntry.key}" {
		group "Publishing"
		description "Publishes all ${artifactsEntry.key} artifacts."
		artifactsEntry.value.each { artifactName ->
			dependsOn "publish${artifactName.replace('.', '').capitalize()}PublicationToMavenRepository"
		}
	}
}
